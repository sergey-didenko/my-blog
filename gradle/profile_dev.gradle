apply plugin: 'org.springframework.boot'
apply plugin: 'com.moowork.node'

dependencies {
    compile "org.springframework.boot:spring-boot-devtools"
}

def profiles = 'dev'
if (project.hasProperty('no-liquibase')) {
    profiles += ',no-liquibase'
}

task webpackBuildDev(type: NpmTask, dependsOn: 'npm_install') {
    onlyIf { shouldWebpackRun() }
    args = ["run", "webpack:build"]
}

processResources {
    filesMatching('**/application.yml') {
        filter {
            it.replace('#project.version#', version)
        }
        filter {
            it.replace('#spring.profiles.active#', profiles)
        }
    }
}

processResources.dependsOn webpackBuildDev
deleteWepApp.dependsOn copyWepApp
war.dependsOn deleteWepApp
copyWar.dependsOn war
bootWar.dependsOn copyWar

def shouldWebpackRun() {
    !file("$buildDir/classes/java/main/static/app/main.bundle.js").exists() || project.hasProperty('webpack')
}
